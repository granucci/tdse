#!/bin/tcsh
#
#================================================================
# TDSE installation 
# -----------------
#
# Normally only the first four settings have to be checked
# and, if necessary, changed:
#
#   FFTW         = version of the FFTW package (ex. fftw-3.3.4).
#                  If it is not found, a slower internal routine 
#                  (singleton) is used for FFT.
#   fc           = fortran compiler (ex. ifort)
#   fflags       = compilations flags for the fortran compiler
#   ldflags      = link flags for the fortran linker
#================================================================
#
#----------------------------------------------------------------
setenv FFTW    fftw-3.3.4
setenv fc      ifort
setenv fflags  -O2
setenv ldflags -static
#----------------------------------------------------------------
#
set cppdefs=""
set fftwinc=""
set fftwlib=""
#
if (!(-e ${FFTW}.tar.gz)) then
   echo " Attempting to get ${FFTW} ...."
   wget http://www.fftw.org/${FFTW}.tar.gz
endif
#
if (-e ${FFTW}.tar.gz) then
   setenv fftw_install $PWD/fftw/${FFTW}_installdir
   mkdir fftw
#
   cd fftw
     tar -zxf ../${FFTW}.tar.gz
     cd $FFTW
       echo " Configuring ${FFTW}  ..."
       ./configure --prefix=${fftw_install} >&install.log
       echo " Building ${FFTW}  ..."
       make >>&install.log
       make install >>&install.log
     cd ..
   cd ..
   set lib=`ls ${fftw_install}|grep lib`
   set cppdefs="-DFFTW3"
   set fftwinc="-I${fftw_install}/include"
   set fftwlib="-L${fftw_install}/${lib} -lfftw3"
#
   set mv=`echo $FFTW|cut -d '-' -f 2|cut -d '.' -f 1`
   if (!(-s ${fftw_install}/${lib}/libfftw${mv}.a)) then
      echo " Problems detected with ${FFTW}  ..."
      echo " The (slower) Singleton module for FFT will be used instead"
      set cppdefs=""
      set fftwinc=""
      set fftwlib=""
   endif
endif
#
# Makefile variables
#
cat <<EOF >make.defs
FC      = $fc
LD      = $fc
FFTWINC = $fftwinc
FFTWLIB = $fftwlib
FFLAGS  = $fflags \$(FFTWINC)
#FFLAGS  = -O0 -C -traceback
LDFLAGS = \$(FFLAGS) \$(FFTWLIB) $ldflags
CPPDEFS = $cppdefs
EOF
#
#
echo " Compiling quant_ev ..."
cd fort
  make
cd ..
